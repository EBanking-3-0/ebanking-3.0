plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id "com.diffplug.spotless" version "6.25.0"
    id "org.sonarqube" version "7.2.2.6593" apply false
}

ext {
    set('springBootVersion', '3.3.5')
    set('springCloudVersion', '2023.0.3')
    set('kafkaVersion', '3.6.1')
    set('lombokVersion', '1.18.36')
    set('mapstructVersion', '1.5.5.Final')
    set('junitVersion', '5.10.1')
    set('mockitoVersion', '5.8.0')
}

allprojects {
    group = 'com.ebanking'
    version = '3.0.0'

    repositories {
        mavenCentral()
        // Fallback mirrors to avoid transient 403s from Maven Central in CI
        maven { url = 'https://repo1.maven.org/maven2' }
        maven { url = 'https://maven-central.storage-download.googleapis.com/maven2' }
        maven { url = 'https://repo.spring.io/milestone' }
    }

    apply plugin: 'com.diffplug.spotless'

    spotless {
        // General configuration if needed
    }

    pluginManager.withPlugin('java') {
        spotless {
            java {
                googleJavaFormat("1.22.0")
                removeUnusedImports()
                trimTrailingWhitespace()
                endWithNewline()
            }
        }
    }

    pluginManager.withPlugin('org.springframework.boot') {
        dependencies {
            implementation 'org.springframework.cloud:spring-cloud-starter-kubernetes-fabric8-all'
        }
    }

    pluginManager.withPlugin('io.spring.dependency-management') {
        dependencyManagement {
            imports {
                mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            }
        }
    }
}
